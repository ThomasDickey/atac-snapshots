dnl Process this file with 'autoconf' to produce a 'configure' script
dnl $Id: configure.in,v 3.6 1997/11/04 01:39:29 tom Exp $
AC_REVISION($Revision: 3.6 $)
AC_INIT(tools/atac)

dnl Normally we'll install in /usr/local, but this will let the user override
dnl once and not be bothered again.
AC_PREFIX_PROGRAM(atacCC)

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_YACC
AC_C_CONST
CF_ANSI_CC_CHECK

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(INCLUDES)
AC_SUBST(DEFINES)
AC_SUBST(LIBS)

AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_HEADERS(\
fcntl.h \
stdlib.h \
sys/time.h \
sys/wait.h \
time.h \
unistd.h \
)

AC_ARG_ENABLE(debugging,
[  --enable-debugging      turn on debug option],,[enableval=no])
if test "$enableval" = no ; then
	CF_STRIP_G_OPT(CFLAGS)
else
	CF_STRIP_O_OPT(CFLAGS)
	CFLAGS="$CFLAGS -g"
fi

AC_ARG_ENABLE(warnings,
[  --enable-warnings       turn on gcc warnings, for debugging ],[
	CF_GCC_WARNINGS
])

AC_TYPE_SIZE_T
AC_TYPE_SIGNAL

AC_CHECK_FUNCS(\
rename \
strerror \
vfprintf \
)

###	Get the host's sizes, in bits, for cexp.y
for td_type in char short int long
do
changequote({,})dnl
td_name="`echo $td_type |tr [a-z] [A-Z]`_TYPE_SIZE"
changequote([,])dnl
AC_MSG_CHECKING(size of $td_type)
td_size="ac_cv_bits_$td_type"
AC_CACHE_VAL($td_size,[
AC_TRY_RUN([#include <stdio.h>
main()
{
	FILE	*f = fopen("conftestval", "w");
	int	count;
	$td_type n;
	if (!f) exit(1);
	for (n = 1, count = 0; n != 0; n = n+n, count++);
	fprintf(f, "%d\n", count);
	exit(0);
}],	[eval 'ac_cv_bits_'$td_type'=`cat conftestval`'],
	[eval 'ac_cv_bits_'$td_type'=unknown'],
	[eval 'ac_cv_bits_'$td_type'=unknown'])
])
[eval 'td_result=$ac_cv_bits_'$td_type]
AC_MSG_RESULT($td_result)
if test $td_result != unknown; then
	AC_DEFINE_UNQUOTED($td_name,$td_result)
	test $td_type = char && AC_DEFINE_UNQUOTED(BITS_PER_UNIT,$td_result)
	changequote({,})dnl
	td_name="HOST_BITS_PER_`echo $td_type |tr [a-z] [A-Z]`"
	changequote([,])dnl
	AC_DEFINE_UNQUOTED($td_name,$td_result)
fi
done

###	If we've got gcc, try to find its machine-name and infer the include-dirs
if test -n "$GCC"; then
	if ( eval "$ac_cv_prog_CC -v" 2>&1 |fgrep 'Reading specs' >conftestval )
	then
		changequote({,})dnl
		td_vers=`eval $ac_cv_prog_CC --version`
		td_temp=`sed -e s'@^[^/]*@@' conftestval`
		td_dir1=`echo $td_temp | sed -e 's@/specs$@/include@'`
		td_dir2=`echo $td_dir1 | sed -e 's@/lib/gcc-lib@@' -e s@/$td_vers/@/@`
		changequote([,])dnl
		AC_MSG_CHECKING(for gcc tool-include)
		if test "$td_dir1" != "$td_temp"; then
			AC_MSG_RESULT($td_dir1)
			AC_DEFINE_UNQUOTED(TOOL_INCLUDE_DIR,"$td_dir1")
		else
			AC_MSG_RESULT(no)
		fi
		AC_MSG_CHECKING(for gcc include)
		if test "$td_dir2" != "$td_dir1"; then
			AC_MSG_RESULT($td_dir2)
			AC_DEFINE_UNQUOTED(GCC_INCLUDE_DIR,"$td_dir2")
		else
			AC_MSG_RESULT(no)
		fi
	fi
	rm -f conftestval
fi

### #define TOOL_INCLUDE_DIR "/usr/lib/gcc-lib/i486-linux/2.7.0/include"
### #define GCC_INCLUDE_DIR "/usr/i486-linux/include"

###	output makefile and config.h
changequote({,})dnl
AC_OUTPUT(makefile
	atac_cpp/makefile
	atac_i/makefile
	atacysis/makefile
	tools/makefile
	tutorial/makefile
	config_h,
{
echo creating config.h
cat >config.h <<cfg_EOF
/*
 * Configuration parameters for ATAC
 * Host: `uname -a`
 * Date: `date`
 */
cfg_EOF
sed	-e '/^# /d' \
	-e 's/ -D/\
#define /g' \
	-e 's/\(#define [A-Za-z_][A-Za-z0-9_]*\)=/\1	/g' \
	-e 's/\\//g' \
	config_h |sort >>config.h
}
echo removing config_h
rm config_h
)
changequote([,])dnl
